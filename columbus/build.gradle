plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.google.protobuf'
}

android {
    compileSdk 35

    defaultConfig {
        minSdk 24
        targetSdk 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        manifestPlaceholders.columbus_permission = rootProject.columbusPermission
        buildConfigField "String", "COLUMBUS_PERMISSION_NAME", "\"$rootProject.columbusPermission\""

    }

    buildTypes {
        release {
            //minify is enabled, but obfuscation is intentionally disabled in the rules
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        aidl true
        buildConfig true
    }
    namespace 'com.google.android.columbus'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.19.2"
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.1'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.13.1'

    //Tensorflow, used in non-CHRE usage
    implementation "org.tensorflow:tensorflow-lite-support:$tensorflow_version"

    //Protobuf
    api "com.google.protobuf:protobuf-javalite:3.25.2"

    compileOnly project(path: ':systemstubs')
    api project(path: ':shared')

}